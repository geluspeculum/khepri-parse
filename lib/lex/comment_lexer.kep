/**
 * @fileOverview Khepri comment lexers.
 */
package (
    singleLineCommentMarker
    singleLineCommentChar
    singleLineCommentChars
    singleLineComment
    
    multiLineCommentStartMarker
    multiLineCommentEndMarker
    multiLineCommentChars
    multiLineComment
   
    comment)
with
    import 'bennu::parse' {
        anyToken
        always
        bind
        cons
        either
        many
        next
        label
        rec
        test
        token},
    import 'bennu::text' {
        character
        string},
    import 'nu-stream::stream' {
        foldl
        NIL},
    
    import './line_terminator_lexer' {lineTerminator}
in {

var join = \p ->
    bind(p, foldl@((+), '') \> always);

/* Lexers
 ******************************************************************************/
// Single Line Comment
////////////////////////////////////////
singleLineCommentMarker = string '//';

singleLineCommentChar = token(
    (!) <\ test@lineTerminator);

singleLineCommentChars = many singleLineCommentChar;

/**
 * Lexer for a single line comment
 * 
 * Returns the contents of the comment.
 */
singleLineComment = label@'Single Line Comment Lexer' <|
    next(
        singleLineCommentMarker,
        join singleLineCommentChars);

// Multi Line Comment
////////////////////////////////////////
multiLineCommentStartMarker = string '/*';

multiLineCommentEndMarker = string '*/';

multiLineCommentChars = label@'Multi Line Comment Characters Lexer' <| rec\self ->
    either(
        next(
            character '*',
            either(
                next(character '/', always NIL),
                cons(always '*', self))),
        cons(anyToken, self));

/**
 * Multi line comment.
 * 
 * Returns the contents of the comment.
 */
multiLineComment = label@'Multi Line Comment Lexer' <|
    next(
        multiLineCommentStartMarker,
        join multiLineCommentChars);

// Comment
////////////////////////////////////////
comment = label@'Comment Lexer' <|
    either(
        singleLineComment,
        multiLineComment);

}