/**
 * @fileOverview Khepri regular expression lexers.
 */
package (
    regularExpressionNonTerminator,
    regularExpressionBackslashSequence,
    
// Class
    regularExpressionClassChar,
    regularExpressionClassChars,
    regularExpressionClass,
    
// Characters
    regularExpressionChar,
    regularExpressionChars,
    regularExpressionFirstChar,
    
// Parts
    regularExpressionFlags,
    regularExpressionBody,
    
// Regex Literal
    regularExpressionLiteral)
with
    import 'bennu::parse' {
        always
        attempt
        bind
        binds
        choice
        cons
        either
        enumeration
        many
        map
        next
        label
        token
        test},
    import 'bennu::lang' {between},
    import 'bennu::text' {character},
    import 'nu-stream::stream' {foldl},
    import './identifier_lexer' {identifierPart},
    import './line_terminator_lexer' {lineTerminator}
in {

var join = foldl @ (+) @ '';

/* Lexers
 ******************************************************************************/
// Parts
////////////////////////////////////////
regularExpressionNonTerminator = token(
    (!) <\ test@lineTerminator);

regularExpressionBackslashSequence = next(
    character '\\',
    map(
        (+, '\\'),
        regularExpressionNonTerminator));

// Class
////////////////////////////////////////
regularExpressionClassChar = either(
    attempt <| token \ tok ->
        !test(lineTerminator, tok) &&
            tok !== ']' && tok !== '\\',
    regularExpressionBackslashSequence);

regularExpressionClassChars = many regularExpressionClassChar;

regularExpressionClass = between(character '[', character ']', 
    map(
        \body ->
            '[' + join(body) + ']',
        regularExpressionClassChars));

// Characters
////////////////////////////////////////
regularExpressionFirstChar = choice(
    token \ tok ->
        !test(lineTerminator, tok) &&
            tok !== '*' && tok !== '\\' && tok !== '`' && tok !== '[',
    regularExpressionBackslashSequence,
    regularExpressionClass);

regularExpressionChar =  choice(
    token(
        \tok ->
            !test(lineTerminator, tok) &&
                tok !== '\\' && tok !== '`' && tok !== '['),
    regularExpressionBackslashSequence,
    regularExpressionClass);

regularExpressionChars = many regularExpressionChar;

// Literal Parts
////////////////////////////////////////
regularExpressionFlags = many identifierPart;

regularExpressionBody = map(
    join,
    cons(
        regularExpressionFirstChar,
        regularExpressionChars));

// Literal
////////////////////////////////////////
/**
 * Lexer for regular expression literals.
 */
regularExpressionLiteral = label@'Regular Expression Lexer' <|
    binds(
        enumeration(
            between(character '`', character '`',
                regularExpressionBody),
            regularExpressionFlags),
        \body, flags -> always(new RegExp(body, join(flags))));

}