/**
 * @fileOverview Khepri lexers.
 */
package (
    literal
    token
    inputElement
    lexer
    lexManyState
    lex)
with
    import 'bennu::parse' parse#{
        always
        attempt
        binds
        bind
        choice
        either
        eof
        getPosition
        enumeration
        extract
        expected
        next
        many
        runState
        Parser
        ParserState},
    import 'bennu::lang' {then},
    import 'nu-stream::stream' {memoStream, NIL, 'from': streamFrom},
    import 'khepri-ast::token' lexToken,
    
    import '../position' {SourceLocation, SourcePosition},
    import './boolean_lexer' {booleanLiteral},
    import './comment_lexer' {comment},
    import './identifier_lexer' {identifier, identifierName},
    import './line_terminator_lexer' {lineTerminator},
    import './null_lexer' {nullLiteral},
    import './number_lexer' {numericLiteral},
    import './punctuator_lexer' {punctuator},
    import './reserved_word_lexer' {reservedWord},
    import './string_lexer' {stringLiteral},
    import './whitespace_lexer' {whitespace},
    import './regular_expression_lexer' {regularExpressionLiteral}
in {

var makeToken = \type, p ->
    bind(p, \value -> always([type, value]));

var buildToken = \p ->
    binds(
        enumeration(
            getPosition,
            p,
            getPosition),
        \start, [type, value], end ->
            always(
                new type(
                    new SourceLocation(start, end),
                    value)));
/* Lexers
 ******************************************************************************/
var literalImpl = choice(
    makeToken(lexToken.StringToken, stringLiteral)
        |> expected@"string literal",
    makeToken(lexToken.RegularExpressionToken, regularExpressionLiteral)
        |> expected@"regular expression literal",
    makeToken(lexToken.BooleanToken, booleanLiteral)
        |> expected@"boolean literal",
    makeToken(lexToken.NullToken, nullLiteral)
        |> expected@"null literal",
    makeToken(lexToken.NumberToken, numericLiteral)
        |> expected@"number literal");

var tokenImpl = choice(
    attempt
        <| makeToken(lexToken.IdentifierToken, identifier)
        |> expected@"identifier",
    attempt
        <| literalImpl,
    attempt
        <| makeToken(lexToken.KeywordToken, reservedWord)
        |> expected@"reserved word",
    makeToken(lexToken.PunctuatorToken, punctuator)
        |> expected@"puctuator");

var inputElementImpl = choice(
    makeToken(lexToken.CommentToken, comment)
        |> expected@"comment",
    makeToken(lexToken.WhitespaceToken, whitespace)
        |> expected@"whitespace",
    makeToken(lexToken.LineTerminatorToken, lineTerminator)
        |> expected@"line terminator",
    tokenImpl);

literal = buildToken(literalImpl);

token = buildToken(tokenImpl);

inputElement = buildToken(inputElementImpl);

lexer = then(
    many(inputElement),
    eof);

/* Running
 ******************************************************************************/
lex = \input -> 
    runState(lexer,
        new ParserState(
            streamFrom(input),
            SourcePosition.initial));

}