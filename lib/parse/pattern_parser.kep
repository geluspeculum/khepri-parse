/**
 * @fileOverview Khepri patterns parsers.
 */
package (
    pattern
    topLevelPattern
    
    identifierPattern
    sinkPattern
    ellipsisPattern
    
    importPattern
    arrayPattern
    objectPattern
    
    argumentList
    argumentsPattern
    
    asPattern
    
    subPattern)
with
    import 'bennu::parse' {
        always
        attempt
        bind
        binds
        choice
        eager
        either
        expected
        enumeration
        next
        optional
        Parser},
    import 'bennu::lang' {between sepBy sepBy1 then},
    import 'khepri-ast::pattern' ast_pattern,
    
    import './common' {node nodea},
    import './token_parser' {keyword punctuator},
    import './value_parser' {identifier stringLiteral}
in {

/* Circular
 ******************************************************************************/
topLevelPattern = \args(...) -> topLevelPattern.apply(undefined, args);

asPattern = \args(...) -> asPattern.apply(undefined, args);

objectPattern = \args(...) -> objectPattern.apply(undefined, args);

subPattern = \args(...) -> subPattern.apply(undefined, args);

/* Parts
 ******************************************************************************/
var sep = optional(null, punctuator ',');

/* Base Patterns
 ******************************************************************************/
// Identifier Pattern
////////////////////////////////////////
identifierPattern = Parser@'Identifier Pattern' <|
    bind(
        identifier,
        \x -> always(ast_pattern.IdentifierPattern.create(x.loc, x)));

// Sink Pattern
////////////////////////////////////////
sinkPattern = Parser@'Sink Pattern' <|
    bind(
        keyword '_',
        \x -> always(ast_pattern.SinkPattern.create(x.loc)));

// Ellipsis Pattern
////////////////////////////////////////
ellipsisPattern = Parser@'Ellipsis Pattern' <|
    bind(
        punctuator '...',
        \x -> always(ast_pattern.EllipsisPattern.create(x.loc)));

/* Unpacks
 ******************************************************************************/
// Array Pattern
////////////////////////////////////////
arrayPattern = Parser@'Array Pattern' <|
    node(
        between(punctuator '[', punctuator ']',
            eager <| sepBy1(sep, topLevelPattern)
                |> expected@"array pattern element"),
        ast_pattern.ArrayPattern.create);

// Object Pattern
////////////////////////////////////////
var objectPatternElement = either(
    nodea(
        enumeration(
            stringLiteral,
            next(
                punctuator ':',
                choice(
                    arrayPattern,
                    objectPattern,
                    asPattern,
                    identifierPattern))),
        ast_pattern.ObjectPatternElement.create),
    node(
        either(
            asPattern,
            identifierPattern),
        \loc, key -> ast_pattern.ObjectPatternElement.create(loc, key, null)));

objectPattern = Parser@'Object Pattern' <|
    node(
        between(punctuator '{', punctuator '}',
            eager <| sepBy1(sep, objectPatternElement)
                |> expected@"object pattern element"),
        ast_pattern.ObjectPattern.create);

// As Pattern
////////////////////////////////////////
asPattern = Parser@'As Pattern' <|
    nodea(
        enumeration(
            attempt <| then(
                identifierPattern,
                punctuator '#'),
            choice(
                arrayPattern,
                objectPattern)
                |> expected@'object or array pattern'),
        ast_pattern.AsPattern.create);

// Import Pattern
////////////////////////////////////////
importPattern = Parser@'Import Pattern' <|
    next(
        keyword 'import',
        nodea(
            enumeration(
                stringLiteral,
                choice(
                    sinkPattern,
                    objectPattern,
                    asPattern,
                    identifierPattern)),
            ast_pattern.ImportPattern.create));

/* Patterns
 ******************************************************************************/
// Top Level Pattern
////////////////////////////////////////
topLevelPattern = Parser@'Top Level Pattern' <|
    choice(
        ellipsisPattern,
        sinkPattern,
        arrayPattern,
        objectPattern,
        asPattern,
        identifierPattern);

// Sub Pattern
////////////////////////////////////////
var subPatternElements = eager <| sepBy1(sep,
    either(
        topLevelPattern,
        subPattern));

subPattern = Parser@'Sub Pattern' <|
    attempt <| nodea(
        enumeration(
            identifierPattern,
            between(punctuator '(', punctuator ')',
                subPatternElements)),
        ast_pattern.ArgumentsPattern.create);

// Arguments Pattern
////////////////////////////////////////
var argumentElements = eager <| sepBy(sep, topLevelPattern);

var selfPattern = next(
    punctuator '=',
    identifierPattern);

argumentList = Parser@'Argument List' <|
    nodea(
        enumeration(
            argumentElements,
            optional(null, selfPattern)),
        \loc, elements, self -> ast_pattern.ArgumentsPattern.create(loc, null, elements, self));

argumentsPattern = Parser@'Arguments Pattern' <|
    either(
        nodea(
            enumeration(
                attempt <| then(
                    optional(null, identifierPattern),
                    punctuator '('),
                then(
                    argumentElements,
                    punctuator ')'),
                optional(null, selfPattern)),
            ast_pattern.ArgumentsPattern.create),
        argumentList);

// Pattern
////////////////////////////////////////
pattern = Parser@'Pattern' <|
    choice(
        importPattern,
        topLevelPattern);

}