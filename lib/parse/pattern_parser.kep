/**
 * @fileOverview Khepri patterns parsers.
 */
package (
    listPattern
    listPattern0
    
    pattern
    unpack
    topLevelPattern
    
    identifierPattern
    sinkPattern
    ellipsisPattern
    
    importPattern
    arrayPattern
    
    objectPatternElement
    objectPattern
    
    asPattern)
with
    import 'bennu::parse' {
        attempt
        append
        bind
        binds
        choice
        cons
        eager
        either
        expected
        enumeration
        next
        optional
        rec
        label
        late},
    import 'bennu::lang' {
        between
        sepBy
        sepBy1
        sepEndBy
        then},
    
    import 'nu-stream::stream' {NIL},
    
    import 'khepri-ast::pattern' ast_pattern,
    
    import './common' {node nodea},
    import './token_parser' {keyword punctuator},
    import './value_parser' {identifier stringLiteral}
in {

var sepEndWith1 = \sep end p ->
    rec\self ->
        cons(
            p,
            optional(NIL,
                next(sep,
                    either(enumeration(end), self))));

var sepEndWith = \sep end p ->
    either(
        enumeration(end),
        sepEndWith1(sep, end, p));

var sepEndWith0 = \sep end p ->
    optional(NIL, sepEndWith(sep, end, p));

/* Forward
 ******************************************************************************/
topLevelPattern = late\-> { return topLevelPattern; };

unpack = late\-> { return unpack; };

/* Parts
 ******************************************************************************/
var sep = optional (punctuator ',');

/* Parts
 ******************************************************************************/
listPattern = \pre mid post  ->
    append(
        sepEndWith(sep, mid, pre),
        next(sep, sepBy(sep, post)));

listPattern0 = \pre mid post  ->
    append(
        sepEndWith0(sep, mid, pre),
        next(sep, sepBy(sep, post)));

/* Base Patterns
 ******************************************************************************/
// Identifier Pattern
////////////////////////////////////////
identifierPattern = label@'Identifier Pattern' <|
    identifier
        .map\x -> ast_pattern.IdentifierPattern.create(x.loc, x);

// Sink Pattern
////////////////////////////////////////
sinkPattern = label@'Sink Pattern' <|
    keyword('_')
        .map\x -> ast_pattern.SinkPattern.create(x.loc);

/* Unpacks
 ******************************************************************************/
// Ellipsis Pattern
////////////////////////////////////////
ellipsisPattern = label@'Ellipsis Pattern' <|
    either(
        between(punctuator '(', punctuator ')',
            punctuator '...')
            .map\x -> ast_pattern.EllipsisPattern.create(x.loc, null),
        
        node(
            next(
                punctuator '...',
                optional identifierPattern),
            ast_pattern.EllipsisPattern.create));

// Array Pattern
////////////////////////////////////////
arrayPattern = label@'Array Pattern' <| let
    element = topLevelPattern
in
    node(
        between(punctuator '[', punctuator ']',
            eager listPattern(
                element |> expected@"array pattern element",
                ellipsisPattern,
                element |> expected@"non-ellipsis array pattern element")),
            ast_pattern.ArrayPattern.create);

// Object Pattern
////////////////////////////////////////
objectPatternElement = either(
    nodea(
        enumeration(
            stringLiteral,
            next(
                punctuator ':',
                unpack)),
        ast_pattern.ObjectPatternElement.create),
    node(
        either(
            asPattern,
            identifierPattern),
        \loc key -> ast_pattern.ObjectPatternElement.create(loc, key, null)));

objectPattern = label@'Object Pattern' <|
    node(
        between(punctuator '{', punctuator '}',
            eager sepBy1(sep,
                objectPatternElement |> expected@"object pattern element")),
        ast_pattern.ObjectPattern.create);

// As Pattern
////////////////////////////////////////
asPattern = label@'As Pattern' <|
    nodea(
        enumeration(
            attempt then(
                identifierPattern,
                punctuator '#'),
            choice(
                arrayPattern,
                objectPattern)
                |> expected@'object or array pattern'),
        ast_pattern.AsPattern.create);

// Import Pattern
////////////////////////////////////////
importPattern = label@'Import Pattern' <|
    next(
        keyword 'import',
        nodea(
            enumeration(
                stringLiteral,
                topLevelPattern),
            ast_pattern.ImportPattern.create));

/* Patterns
 ******************************************************************************/
// Unpack
////////////////////////////////////////
unpack = label@'Unpack' <|
    choice(
        arrayPattern,
        objectPattern,
        asPattern,
        identifierPattern);

// Top Level Pattern
////////////////////////////////////////
topLevelPattern = label@'Top Level Pattern' <|
    choice(
        sinkPattern,
        unpack);

}