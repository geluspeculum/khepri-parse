/**
 * @fileOverview Parsers for working with lex tokens.
 */
package (
    punctuator
    anyPunctuator
    operator
    anyOperator
    
    keyword
    identifier
    anyIdentifier
    
    nullLiteral
    booleanLiteral
    numericLiteral
    stringLiteral
    regularExpressionLiteral
    
    prefixedOp)
with
    import 'bennu::parse' {
        ExpectError
        token}
in {

var indexOf = Function.prototype.call.bind(Array.prototype.indexOf);

var join = Function.prototype.call.bind(Array.prototype.join);

var expectError = \msg ->
    \pos, tok ->
        new ExpectError(pos, msg, ?tok : tok.value : "end of input");

var typeParser = \type, msg ->
    token(
        \tok -> (tok.type === type),
        expectError(msg));

var selectAny = \type ->
    \options(...) ->
        token(
            \tok -> (tok.type === type && indexOf(options, tok.value) >= 0),
            expectError(join(options, ', ')));

/* Parsers
 ******************************************************************************/
punctuator = selectAny 'Punctuator' ;

anyPunctuator = typeParser('Punctuator', 'any punctuator');

operator = selectAny 'Operator' ;

anyOperator  = typeParser('Operator', 'any operator');

keyword = selectAny 'Keyword';

anyIdentifier = typeParser('Identifier', 'any identifier');

identifier = selectAny 'Identifier';

nullLiteral = typeParser('Null', 'Null literal');

booleanLiteral = typeParser('Boolean', 'boolean literal');

numericLiteral = typeParser('Number', 'numeric literal');

stringLiteral = typeParser('String', 'string literal');

regularExpressionLiteral = typeParser('RegularExpression', 'regular expression literal');

/* Parsers
 ******************************************************************************/
var startsWith := \choices value ->
    Array.prototype.some.call(choices,
        String.prototype.indexOf.bind(value) \> (===, 0));

prefixedOp := \options(...) ->
    token \ tok -> 
        (tok.type === 'Operator' &&
        (startsWith(options, tok.value)));

}