/**
 * @fileOverview Program parsers.
 */
package (
    sourceElement
    sourceElements
    program)
with
    import 'bennu::parse' {eager either eof many label late},
    import 'bennu::lang' {then},
    
    import 'khepri-ast::program' ast_program,
    
    import './common' {node},
    import './statement_parser' _,
    import './package_parser' _
in {

/* Circular
 ******************************************************************************/
var statement = late\-> {
    with
        import './statement_parser' {statement}
    in {
        return statement;
    }
};

var khepriPackage = late\-> {
    with
        import './package_parser' {khepriPackage}
    in {
        return khepriPackage;
    }
};

/* Parsers
 ******************************************************************************/
sourceElement = statement;

sourceElements = eager <| many sourceElement;

program = label@'Program' <|
    node(
        then(
            either(
                khepriPackage,
                sourceElements),
            eof),
        ast_program.Program.create);

}