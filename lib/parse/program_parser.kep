/**
 * @fileOverview Khepri program parsers.
 */
package (
    sourceElement,
    sourceElements,
    program)
with
    import 'bennu::parse' {always, cons, eager, either, eof, many, next, Parser, rec},
    import 'bennu::lang' {then},
    import 'nu-stream::stream' {NIL},
    import 'khepri-ast::program' ast_program,
    
    import './common' {node},
    import './statement_parser' _,
    import './package_parser' _
in {

/* Circular
 ******************************************************************************/
var statementParser = \args(...) -> {
    with
        import './statement_parser' {statement}
    in {
        return statement.apply(undefined, args);
    }
};

var khepriPackage = \args(...) -> {
    with
        import './package_parser' {khepriPackage}
    in {
        return khepriPackage.apply(undefined, args);
    }
};

/* Parsers
 ******************************************************************************/
/**
 * Parser for an ECMAScript source element.
 * 
 * Source Elements are top level nodes that makes up a program.
 */
sourceElement = statementParser;

/**
 * Parser for a enumeration of zero or more source elements.
 */
sourceElements = eager <| many sourceElement;

/**
 * Parser for an ECMAScript 5.1 program.
 */
program = Parser@'Program' <|
    node(
        either(
            then(khepriPackage, eof),
            eager <| rec(\self ->
                either(
                    next(eof, always(NIL)),
                    cons(sourceElement, self)))),
        ast_program.Program.create);

}