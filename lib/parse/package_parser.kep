/**
 * @fileOverview Package parsers.
 */
package (
    khepriPackage)
with
    import 'bennu::parse' {
        eager
        either
        enumeration
        expected
        next
        optional
        label
        late},
    import 'bennu::lang' {between, sepBy},
    import 'khepri-ast::package' ast_package,
    
    import './common' {node, nodea},
    import './token_parser' {keyword punctuator},
    import './value_parser' {identifier stringLiteral},
    import './statement_parser' {withStatement blockStatement}
in {

/* labels
 ******************************************************************************/
var packageExport = label@'Package Export' <|
    either(
        node(
            identifier,
            ast_package.PackageExport.create),
        nodea(
            enumeration(
                stringLiteral,
                next(
                    punctuator ':',
                    identifier)),
            \loc alias id ->
                ast_package.PackageExport.create(loc, id, alias)));

var packageExportList = label@'Package Export List' <|
    node(
        between(punctuator '(', punctuator ')', 
            eager <| sepBy(optional (punctuator ','),
                packageExport)),
        ast_package.PackageExports.create);

var packageExports = label@'Package Exports' <|
    either(
        packageExportList,
        node(
            identifier,
            ast_package.PackageExport.create));

var packageBody = label@'Package Body' <|
    either(
        withStatement,
        blockStatement);

khepriPackage = label@'Package' <|
    nodea(
        next(
            keyword 'package',
            enumeration(
                packageExports |> expected@"package exports",
                packageBody |> expected@"package body")),
            ast_package.Package.create);

}